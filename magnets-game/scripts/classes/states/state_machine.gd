class_name StateMachine
extends Node

@export var initial_state: State
var current_state: State
var states: Dictionary = {}


func _ready() -> void:
	if not initial_state:
		printerr("initial_state is null")
	
	for child in get_children():
		if child is State:
			states[child.name.to_lower()] = child
			child.transitioned.connect(on_state_transitioned)
	
	if initial_state:
		initial_state.enter()
		current_state = initial_state


func on_state_transitioned(state, new_state_name) -> void:
	if state != current_state:
		return
	
	var new_state = states.get(new_state_name.to_lower())
	if not new_state:
		return
	
	current_state.exit()
	new_state.enter()
	current_state = new_state


func _physics_process(delta):
	if current_state:
		current_state.physics_update(delta)
